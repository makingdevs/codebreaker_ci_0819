buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		// if you use the version of cucumber-jvm that the plugin was built with
		classpath "info.cukes:cucumber-core:1.2.5"
		classpath "com.github.samueltbrown:gradle-cucumber-plugin:0.9"
	}
}

plugins {
	id 'org.springframework.boot' version '2.1.6.RELEASE'
	id 'groovy'
    id "com.github.samueltbrown.cucumber" version "0.9"
    id "com.energizedwork.webdriver-binaries" version "1.4"
}

apply plugin: 'io.spring.dependency-management'

group = 'com.makingdevs.codebreaker'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

ext {
	gebVersion = '3.0.1'
	seleniumVersion = '3.14.0'
	cucumberJvmVersion = '1.2.5'
	chromeDriverVersion = '73.0.3683.68'
	geckoDriverVersion = '0.24.0'
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:2.5.6"
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'org.junit.jupiter:junit-jupiter:5.4.0'
	testCompile "org.gebish:geb-core:$gebVersion"
	testCompile "org.seleniumhq.selenium:selenium-api:$seleniumVersion"
	testCompile "info.cukes:cucumber-core:$cucumberJvmVersion"
	testCompile "info.cukes:cucumber-groovy:$cucumberJvmVersion"
	testCompile "io.jdev.geb:geb-cucumber:0.3"
	testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

webdriverBinaries {
	chromedriver chromeDriverVersion
	geckodriver geckoDriverVersion
}

cucumber {
	formats = [
		'pretty', // prints nice format out to the console
		'html:build/reports/cucumber', // html
		'junit:build/cucumber.xml' // junit format for integration with CI tool etc
	]
	// src/cucumber/resources is included automatically
	glueDirs = [
		"classpath:io.jdev.geb.cucumber.steps.groovy.en"
	]
}

configureChromeDriverBinary {
	addBinaryAware { path ->
		tasks.cucumber.jvmOptions.systemProperties("webdriver.chrome.driver": path)
	}
}

configureGeckoDriverBinary {
	addBinaryAware { path ->
		tasks.cucumber.jvmOptions.systemProperties("webdriver.gecko.driver": path)
	}
}

tasks.cucumber {
	dependsOn configureChromeDriverBinary, configureGeckoDriverBinary
	jvmOptions.systemProperties([
		"geb.cucumber.step.packages": "pages",
		"geb.env": System.getProperty("geb.env")
	])
}

tasks.withType(GroovyCompile) {
	groovyOptions.forkOptions.memoryMaximumSize = '256m'
}
